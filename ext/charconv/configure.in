dnl
dnl Example of configuring Gauche extension
dnl
dnl $Id: configure.in,v 1.13 2002-02-07 10:33:51 shirok Exp $
AC_INIT(charconv.c)

dnl Determine if we're configuring inside of Gauche main source tree or not.
if test -f ../../src/gauche.h; then
  GAUCHE_CONFIG="sh ../../src/gauche-config"
  GAUCHE_TOP='../../'
  GAUCHE_INC="-I../../src -I../../gc/include `$GAUCHE_CONFIG --local-incdir`"
  GAUCHE_LIB="-L../../src"
  GOSH="../../src/gosh -I../../src -I../../lib"
else
  GAUCHE_CONFIG=gauche-config
  GAUCHE_TOP=
  GAUCHE_INC="`gauche-config -I`"
  GAUCHE_LIB="`gauche-config -L`"
  GOSH=gosh
fi
AC_SUBST(GAUCHE_CONFIG)
AC_SUBST(GAUCHE_TOP)
AC_SUBST(GAUCHE_INC)
AC_SUBST(GOSH)

dnl A user has a choice to install the module into Gauche system directory
dnl or site local directory, by setting environment variable INSTALL_TYPE
dnl to either 'sys' or 'site'.   The variable can be overridden at make time.
: ${INSTALL_TYPE=sys}  dnl This sets the default.
AC_SUBST(INSTALL_TYPE)

dnl Process extra configure argument
ICONV_NAME=iconv
AC_ARG_WITH(iconv, , [
  case $with_iconv in
    no|yes) iconv_lib="" ;;
    *)   CPPFLAGS="$CPPFLAGS -I$with_iconv/include";
         LDFLAGS="$LDFLAGS -L$with_iconv/lib";
         iconv_lib="-liconv";;
  esac], [with_iconv=yes])
AC_ARG_WITH(iconv-lib, , [ ICONV_NAME="$with_iconv_lib"; iconv_lib="-l$with_iconv_lib" ])
AC_SUBST(ICONV_NAME)
LIBS="$LIBS $iconv_lib"

dnl Get compiler parameters which Gauche has been compiled with.
CC="`$GAUCHE_CONFIG --cc`"
AC_SUBST(CC)

CPPFLAGS="$GAUCHE_INC $CPPFLAGS"
CFLAGS="$CFLAGS `$GAUCHE_CONFIG --so-cflags` $CPPFLAGS"
AC_SUBST(CFLAGS)
LDFLAGS="$LDFLAGS `$GAUCHE_CONFIG --local-libdir`"

dnl Check for other programs.
AC_PROG_INSTALL

dnl Add more test
if test $with_iconv != no; then
  AC_CHECK_HEADER($ICONV_NAME.h, AC_DEFINE(HAVE_ICONV_H))
fi

dnl Check the iconv() prototype
ichdr="#include <$ICONV_NAME.h>"
AC_CACHE_CHECK(iconv takes const char **input, ac_cv_iconv_const_input, [
AC_TRY_COMPILE($ichdr, [
const char *inbuf;
char *outbuf;
int inroom, outroom;
iconv((iconv_t)0, &inbuf, &inroom, &outbuf, &outroom);
], ac_cv_iconv_const_input=yes, ac_cv_iconv_const_input=no)])

if test "$ac_cv_iconv_const_input" = yes; then
AC_DEFINE(ICONV_CONST_INPUT)
fi

dnl Set LDFLAGS to generate shared library.
dnl This has to come after all the tests that requre linking, or those test
dnl will fail because they can't generate stand-alone executable.
LDFLAGS="$LDFLAGS `$GAUCHE_CONFIG --so-ldflags`"
LIBS="$GAUCHE_LIB `$GAUCHE_CONFIG -l`"
AC_SUBST(LDFLAGS)

dnl Output
AC_OUTPUT(Makefile charconv.h)
