dnl
dnl Configuring gauche.charconv extension
dnl  process this file with "autoconf -l ../.." to generate 'configure'.
dnl $Id: configure.in,v 1.14 2002-02-10 05:40:04 shirok Exp $
AC_INIT(charconv.c)
AC_GAUCHE_INIT_EXT

# Sets the default value of INSTALL_TYPE macro.
AC_GAUCHE_INSTALL_TYPE(sys)

# Process extra configure argument
ICONV_NAME=iconv
AC_ARG_WITH(iconv, , [
  case $with_iconv in
    no|yes) iconv_lib="" ;;
    *)   CPPFLAGS="$CPPFLAGS -I$with_iconv/include";
         LDFLAGS="$LDFLAGS -L$with_iconv/lib";
         iconv_lib="-liconv";;
  esac], [with_iconv=yes])
AC_ARG_WITH(iconv-lib, , [ ICONV_NAME="$with_iconv_lib"; iconv_lib="-l$with_iconv_lib" ])
AC_SUBST(ICONV_NAME)
LIBS="$LIBS $iconv_lib"

# Get compiler parameters which Gauche has been compiled with.
AC_GAUCHE_CC
AC_GAUCHE_FLAGS

# Check for other programs.
AC_PROG_INSTALL

# Add more test
if test $with_iconv != no; then
  AC_CHECK_HEADER($ICONV_NAME.h, AC_DEFINE(HAVE_ICONV_H))
fi

# Check the iconv() prototype
ichdr="#include <$ICONV_NAME.h>"
AC_CACHE_CHECK(iconv takes const char **input, ac_cv_iconv_const_input, [
AC_TRY_COMPILE($ichdr, [
const char *inbuf;
char *outbuf;
int inroom, outroom;
iconv((iconv_t)0, &inbuf, &inroom, &outbuf, &outroom);
], ac_cv_iconv_const_input=yes, ac_cv_iconv_const_input=no)])

if test "$ac_cv_iconv_const_input" = yes; then
AC_DEFINE(ICONV_CONST_INPUT)
fi

# Set LDFLAGS to generate shared library.
AC_GAUCHE_FIX_LIBS

# Output
AC_GAUCHE_EXT_FIXUP(charconv)
AC_OUTPUT(Makefile charconv.h)
