dnl
dnl Example of configuring Gauche extension
dnl
dnl $Id: configure.in,v 1.3 2001-06-02 21:13:09 shirok Exp $
AC_INIT(charconv.c)

dnl Determine if we're configuring inside of Gauche main source tree or not.
if test -f ../../src/gauche.h; then
  GAUCHE_CONFIG="sh ../../src/gauche-config"
  GAUCHE_TOP='../../'
  GAUCHE_INC="-I../../src -I../../gc"
  GOSH="../../src/gosh -I../../src -I../../lib"
else
  GAUCHE_CONFIG=gauche-config
  GAUCHE_TOP=
  GAUCHE_INC=`gauche-config -I`
  GOSH=gosh
fi
AC_SUBST(GAUCHE_CONFIG)
AC_SUBST(GAUCHE_TOP)
AC_SUBST(GAUCHE_INC)
AC_SUBST(GOSH)

dnl A user has a choice to install the module into Gauche system directory
dnl or site local directory, by setting environment variable INSTALL_TYPE
dnl to either 'sys' or 'site'.   The variable can be overridden at make time.
: ${INSTALL_TYPE=site}  dnl This sets the default.
AC_SUBST(INSTALL_TYPE)

dnl Process extra configure argument
AC_ARG_WITH(iconv, , [
  case $with_iconv in
    no|yes) iconv_lib="" ;;
    *)   CPPFLAGS="$CPPFLAGS -I$with_iconv"; iconv_lib="-liconv" ;;
  esac], [with_iconv=yes])
AC_ARG_WITH(iconv-lib, , [ iconv_lib="-l$with_iconv_lib" ])
LIBS="$LIBS $iconv_lib"  

dnl Get compiler parameters which Gauche has been compiled with.
CC="`$GAUCHE_CONFIG --cc`"
AC_SUBST(CC)

CFLAGS="$CFLAGS $GAUCHE_INC `$GAUCHE_CONFIG --so-cflags` $CPPFLAGS"

dnl Check for other programs.
AC_PROG_INSTALL

dnl Add more test
if test $with_iconv != no; then
  AC_CHECK_HEADERS(iconv.h)
fi

dnl Set LDFLAGS to generate shared library.
dnl This has to come after all the tests that requre linking, or those test
dnl will fail because they can't generate stand-alone executable.
LDFLAGS="$LDFLAGS `$GAUCHE_CONFIG --so-ldflags`"

dnl Output
AC_OUTPUT(Makefile)
