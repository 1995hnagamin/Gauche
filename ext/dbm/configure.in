dnl
dnl Configuring gauche.dbm extension
dnl  process this file with "autoconf -l ../.." to generate 'configure'.
dnl $Id: configure.in,v 1.16 2003-04-12 03:40:57 shirok Exp $
AC_INIT(dbm.scm)
AC_CONFIG_HEADER(dbmconfig.h)
AC_CANONICAL_SYSTEM
AC_GAUCHE_INIT_EXT

# Sets the default value of INSTALL_TYPE macro.
AC_GAUCHE_INSTALL_TYPE(sys)

# Get compiler parameters which Gauche has been compiled with.
AC_GAUCHE_CC
AC_GAUCHE_FLAGS

# Check for other programs.
AC_PROG_INSTALL
AC_PROG_LIBTOOL

# Check various dbm libraries availability
# On some systems, legacy DBM and NDBM is emulated by more powerful
# packages like BSD DB or GDBM.  We need to be careful to pick the
# consistent header/library pair.
AC_CHECK_HEADERS(gdbm.h, [
  ARCHFILES=gdbm.la
  SCMFILES=gdbm.scm
])
AC_CHECK_HEADERS(ndbm.h gdbm/ndbm.h, [
  NDBM_HEADER=$ac_header
  ARCHFILES="ndbm.la $ARCHFILES"
  SCMFILES="ndbm.scm $SCMFILES"
])
AC_CHECK_HEADERS(dbm.h gdbm/dbm.h, [
  ODBM_HEADER=$ac_header
  ARCHFILES="odbm.la $ARCHFILES"
  SCMFILES="odbm.scm $SCMFILES"
])
#AC_CHECK_HEADERS(db.h, [
#  BDBM_HEADER=$ac_header
#  ARCHFILES="bsddb.la $ARCHFILES"
#  SCMFILES="bsddb.scm $SCMFILES"
#])

AC_CHECK_LIB(gdbm, gdbm_open, [ GDBMLIB="-lgdbm" ])
LIBSAVE="$LIBS"
if test "$NDBM_HEADER" = "gdbm/ndbm.h"; then
  NDBMLIB="-lgdbm"  # use emulation by gdbm
else
  AC_SEARCH_LIBS(dbm_open, ndbm gdbm,   [ NDBMLIB="$LIBS"; LIBS="$LIBSAVE" ])
fi
if test "$ODBM_HEADER" = "gdbm/dbm.h"; then
  ODBMLIB="-lgdbm"  # use emulation by gdbm
else
  AC_SEARCH_LIBS(dbminit,  dbm gdbm, [ ODBMLIB="$LIBS"; LIBS="$LIBSAVE" ])
fi
#AC_CHECK_LIB(db, dbopen, [ BSDDBLIB="-ldb" ])

AC_SUBST(ARCHFILES)
AC_SUBST(SCMFILES)
AC_SUBST(GDBMLIB)
AC_SUBST(NDBMLIB)
AC_SUBST(ODBMLIB)
#AC_SUBST(BSDDBLIB)

# Set LDFLAGS to generate shared library.
AC_GAUCHE_FIX_LIBS

# Output
AC_GAUCHE_EXT_FIXUP(gdbm)
AC_GAUCHE_EXT_FIXUP(ndbm)
AC_GAUCHE_EXT_FIXUP(odbm)
#AC_GAUCHE_EXT_FIXUP(bsddb)
AC_OUTPUT(Makefile)
