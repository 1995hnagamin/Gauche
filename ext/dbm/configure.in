dnl
dnl Configuring gauche.dbm extension
dnl
dnl $Id: configure.in,v 1.7 2001-11-09 08:28:40 shirok Exp $
AC_INIT(dbm.scm)
AC_CONFIG_HEADER(dbmconfig.h)

dnl Determine if we're configuring inside of Gauche main source tree or not.
if test -f ../../src/gauche.h; then
  GAUCHE_CONFIG="sh ../../src/gauche-config"
  GAUCHE_TOP='../../'
  GAUCHE_INC="-I../../src -I../../gc/include `$GAUCHE_CONFIG --local-incdir`"
  GOSH="../../src/gosh -I../../src -I../../lib"
else
  GAUCHE_CONFIG=gauche-config
  GAUCHE_TOP=
  GAUCHE_INC=`gauche-config -I`
  GOSH=gosh
fi
AC_SUBST(GAUCHE_CONFIG)
AC_SUBST(GAUCHE_TOP)
AC_SUBST(GAUCHE_INC)
AC_SUBST(GOSH)

dnl A user has a choice to install the module into Gauche system directory
dnl or site local directory, by setting environment variable INSTALL_TYPE
dnl to either 'sys' or 'site'.   The variable can be overridden at make time.
: ${INSTALL_TYPE=sys}  dnl This sets the default.
AC_SUBST(INSTALL_TYPE)

dnl Get compiler parameters which Gauche has been compiled with.
CC="`$GAUCHE_CONFIG --cc`"
AC_SUBST(CC)

CFLAGS="$CFLAGS $GAUCHE_INC `$GAUCHE_CONFIG --so-cflags`"
AC_SUBST(CFLAGS)
CPPFLAGS="$GAUCHE_INC"       # some test requires this
LDFLAGS="$LDFLAGS `$GAUCHE_CONFIG --local-libdir`"

dnl Check for other programs.
AC_PROG_INSTALL

dnl Check various dbm libraries availability
AC_CHECK_HEADERS(gdbm.h, [
  ARCHFILES=gdbm.so
  SCMFILES=gdbm.scm
])
AC_CHECK_HEADERS(ndbm.h gdbm/ndbm.h, [
  ARCHFILES="ndbm.so $ARCHFILES"
  SCMFILES="ndbm.scm $SCMFILES"
])
AC_CHECK_HEADERS(dbm.h gdbm/dbm.h, [
  ARCHFILES="odbm.so $ARCHFILES"
  SCMFILES="odbm.scm $SCMFILES"
])

AC_CHECK_LIB(gdbm, gdbm_open, [ GDBMLIB="-lgdbm" ])
AC_CHECK_LIB(ndbm, ndbm_open, [ NDBMLIB="-lndbm" ])
if test -z "$NDBMLIB"; then
  AC_CHECK_LIB(gdbm, dbm_open, [ NDBMLIB="-lgdbm" ])
fi
AC_CHECK_LIB(dbm, dbminit,  [ ODBMLIB="-ldbm" ])
if test -z "$ODBMLIB"; then
  AC_CHECK_LIB(gdbm, dbminit, [ ODBMLIB="-lgdbm" ])
fi

AC_SUBST(ARCHFILES)
AC_SUBST(SCMFILES)
AC_SUBST(GDBMLIB)
AC_SUBST(NDBMLIB)
AC_SUBST(ODBMLIB)

dnl Set LDFLAGS to generate shared library.
dnl This has to come after all the tests that requre linking, or those test
dnl will fail because they can't generate stand-alone executable.
LDFLAGS="$LDFLAGS `$GAUCHE_CONFIG --so-ldflags`"
AC_SUBST(LDFLAGS)

dnl Output
AC_OUTPUT(Makefile)
