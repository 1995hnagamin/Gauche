dnl
dnl Configuring gauche.dbm extension
dnl
dnl $Id: configure.in,v 1.11 2001-12-14 20:38:56 shirok Exp $
AC_INIT(dbm.scm)
AC_CONFIG_HEADER(dbmconfig.h)

dnl Determine if we're configuring inside of Gauche main source tree or not.
if test -f ../../src/gauche.h; then
  GAUCHE_CONFIG="sh ../../src/gauche-config"
  GAUCHE_TOP='../../'
  GAUCHE_INC="-I../../src -I../../gc/include `$GAUCHE_CONFIG --local-incdir`"
  GOSH="../../src/gosh -I../../src -I../../lib"
else
  GAUCHE_CONFIG=gauche-config
  GAUCHE_TOP=
  GAUCHE_INC=`gauche-config -I`
  GOSH=gosh
fi
AC_SUBST(GAUCHE_CONFIG)
AC_SUBST(GAUCHE_TOP)
AC_SUBST(GAUCHE_INC)
AC_SUBST(GOSH)

dnl A user has a choice to install the module into Gauche system directory
dnl or site local directory, by setting environment variable INSTALL_TYPE
dnl to either 'sys' or 'site'.   The variable can be overridden at make time.
: ${INSTALL_TYPE=sys}  dnl This sets the default.
AC_SUBST(INSTALL_TYPE)

dnl Get compiler parameters which Gauche has been compiled with.
CC="`$GAUCHE_CONFIG --cc`"
AC_SUBST(CC)

CFLAGS="$CFLAGS $GAUCHE_INC `$GAUCHE_CONFIG --so-cflags`"
AC_SUBST(CFLAGS)
CPPFLAGS="$GAUCHE_INC"       # some test requires this
LDFLAGS="$LDFLAGS `$GAUCHE_CONFIG --local-libdir`"

dnl Check for other programs.
AC_PROG_INSTALL

dnl Check various dbm libraries availability
dnl On some systems, legacy DBM and NDBM is emulated by more powerful
dnl packages like BSD DB or GDBM.  We need to be careful to pick the
dnl consistent header/library pair.
AC_CHECK_HEADERS(gdbm.h, [
  ARCHFILES=gdbm.so
  SCMFILES=gdbm.scm
])
AC_CHECK_HEADERS(ndbm.h gdbm/ndbm.h, [
  NDBM_HEADER=$ac_header
  ARCHFILES="ndbm.so $ARCHFILES"
  SCMFILES="ndbm.scm $SCMFILES"
])
AC_CHECK_HEADERS(dbm.h gdbm/dbm.h, [
  ODBM_HEADER=$ac_header
  ARCHFILES="odbm.so $ARCHFILES"
  SCMFILES="odbm.scm $SCMFILES"
])
#AC_CHECK_HEADERS(db.h, [
#  BDBM_HEADER=$ac_header
#  ARCHFILES="bdbm.so $ARCHFILES"
#  SCMFILES="bdbm.scm $SCMFILES"
#])

AC_CHECK_LIB(gdbm, gdbm_open, [ GDBMLIB="-lgdbm" ])
LIBSAVE="$LIBS"
if test "$NDBM_HEADER" = "gdbm/ndbm.h"; then
  NDBMLIB="-lgdbm"  # use emulation by gdbm
else
  AC_SEARCH_LIBS(dbm_open, ndbm gdbm,   [ NDBMLIB="$LIBS"; LIBS="$LIBSAVE" ])
fi
if test "$ODBM_HEADER" = "gdbm/dbm.h"; then
  ODBMLIB="-lgdbm"  # use emulation by gdbm
else
  AC_SEARCH_LIBS(dbminit,  dbm gdbm, [ ODBMLIB="$LIBS"; LIBS="$LIBSAVE" ])
fi
#AC_CHECK_LIB(db, dbopen, [ BDBMLIB="-ldb" ])

AC_SUBST(ARCHFILES)
AC_SUBST(SCMFILES)
AC_SUBST(GDBMLIB)
AC_SUBST(NDBMLIB)
AC_SUBST(ODBMLIB)
#AC_SUBST(BDBMLIB)

dnl Set LDFLAGS to generate shared library.
dnl This has to come after all the tests that requre linking, or those test
dnl will fail because they can't generate stand-alone executable.
LDFLAGS="$LDFLAGS `$GAUCHE_CONFIG --so-ldflags`"
AC_SUBST(LDFLAGS)

dnl Output
AC_OUTPUT(Makefile)
